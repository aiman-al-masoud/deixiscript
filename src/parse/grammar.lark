// AST ---------------------------------------------------------
?ast: sentence | noun_phrase | number | _parens

number:NUMBER

// parentheses
_parens: "(" ast ")"

// SENTENCE ----------------------------------------------------
?sentence: compound_sentence | simple_sentence

// COMPOUND (AND ALSO COMPLEX) SENTENCE ------------------------
compound_sentence: left op right
left: ast
right: ast
op: BINOP

// SIMPLE-SENTENCE ----------------------------------------------
simple_sentence: _command | _question1 | _question2
_question1: "does" subject "do" verb object? complement* "?"?
_question2: subject? "does" verb object? complement* "?"
_command: subject? cmd verb object? complement*
cmd:DOES_CMD
subject: noun_phrase
verb: NAME
object: noun_phrase
?complement: preposition complement_head?
complement_head: noun_phrase
preposition: PREPOSITION

// NOUN-PHRASE --------------------------------------------------
?noun_phrase: noun | relative | _parens

relative: relative_head "which" subordinate
relative_head:noun_phrase
subordinate: sentence

noun: (_ARTICLE?|ord?|noun_cmd?|card?|negation?)* noun_head
noun_head: NAME
card: NUMBER
ord: ORDINAL
noun_cmd:"new"

// TERMINALS ----------------------------------------------------
negation: "not"
NAME: /[a-z]+/ | "_"
NUMBER: /[0-9]+/
ORDINAL: "first" | "last"
BINOP: "and" | "or" | "when" | "after" | "+" | "-" | "*" | "/"
PREPOSITION: "to" | "as" | "from" | "on"
_ARTICLE:"the"|"an"|"a"
DOES_CMD: "does"
%import common.WS
%ignore WS