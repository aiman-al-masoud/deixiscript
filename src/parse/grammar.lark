// AST ---------------------------------------------------------
?ast: sentence | noun_phrase

// SENTENCE ----------------------------------------------------
?sentence: compound_sentence | simple_sentence

// COMPOUND (AND ALSO COMPLEX) SENTENCE ------------------------
compound_sentence: ast BINOP ast

// SIMPLE-SENTENCE ----------------------------------------------
simple_sentence: _command | _question
_question: subject? _DOES NEGATION* verb object? complement* "?"
_command: subject? cmd NEGATION* verb object? complement*
cmd:_DOES
subject: noun_phrase
verb: NAME
object: noun_phrase
?complement: PREPOSITION noun_phrase?

// NOUN-PHRASE --------------------------------------------------
?noun_phrase: noun_article | noun_adjective | noun | noun_relative | _parens | STRING | NUMBER

noun_relative: noun_phrase _RELATIVE sentence
noun_adjective: adjective noun_phrase
?noun_article: _ARTICLE noun_phrase

// noun
noun: (ord?|noun_cmd?|card?|NEGATION?)* noun_head complement*
noun_head: NAME
card: NUMBER
ord: ORDINAL
noun_cmd:"new"
adjective: /([a-z]+)ful/

// parentheses
_parens: "(" ast ")"

// TERMINALS ----------------------------------------------------
NEGATION: "not"
NAME: /[a-z]+/
NUMBER: /[0-9]+/
ORDINAL: "first" | "last"
BINOP: "and" | "or" | "when" | "after" | "+" | "-" | "*" | "/"
PREPOSITION: "to" | "as" | "on" | "of"
_ARTICLE:"the"|"an"|"a"
_DOES: "does" //|"do"
_RELATIVE:"which"
%import common.WS
%import common.ESCAPED_STRING -> STRING
%ignore WS