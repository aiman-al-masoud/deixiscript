// AST ---------------------------------------------------------
?ast: sentence | noun_phrase

// SENTENCE ----------------------------------------------------
?sentence: compound_sentence | simple_sentence

// COMPOUND (AND ALSO COMPLEX) SENTENCE ------------------------
compound_sentence: ast BINOP ast

// SIMPLE-SENTENCE ----------------------------------------------
simple_sentence: noun_phrase? _HELPER NEGATION? VERB noun_phrase? complement* QUESTION_MARK?
complement: PREPOSITION noun_phrase?

// NOUN-PHRASE --------------------------------------------------
?noun_phrase: noun | STRING | NUMBER 
        | noun_article 
        | noun_negation 
        | noun_ordinal 
        | noun_cardinal 
        | noun_complement 
        | noun_adjective 
        | noun_relative 
        | _parens

noun_relative: noun_phrase _RELATIVE sentence
noun_adjective: ADJECTIVE noun_phrase
?noun_article: _ARTICLE noun_phrase
noun_negation: NEGATION noun_phrase
noun_complement: noun_phrase complement
noun_ordinal: ORDINAL noun_phrase
noun_cardinal: NUMBER noun_phrase
_parens: "(" ast ")"
noun: WORD

// TERMINALS ----------------------------------------------------
NEGATION: "not"
VERB: WORD
WORD: /[a-z]+/
NUMBER: /[0-9]+/
ADJECTIVE: /([a-z]+)ful/
ORDINAL: "first" | "last" | "new"
BINOP: "and" | "or" | "when" | "after" | "+" | "-" | "*" | "/"
PREPOSITION: "to" | "as" | "on" | "of"
QUESTION_MARK: "?"
_ARTICLE:"the"|"an"|"a"
_HELPER: "does" //|"do"
_RELATIVE:"which"|"that"|"who"
%import common.WS
%import common.ESCAPED_STRING -> STRING
%ignore WS