// AST ---------------------------------------------------------
?ast: sentence | noun_phrase

// SENTENCE ----------------------------------------------------
?sentence: compound_sentence | simple_sentence

// COMPOUND (AND ALSO COMPLEX) SENTENCE ------------------------
compound_sentence: left op right
left: ast
right: ast
op: BINOP

// SIMPLE-SENTENCE ----------------------------------------------
simple_sentence: _command | _question
_question: subject? DOES negation* verb object? complement* "?"
_command: subject? cmd negation* verb object? complement*
cmd:DOES
subject: noun_phrase
verb: NAME
object: noun_phrase
?complement: preposition complement_head?
complement_head: noun_phrase
preposition: PREPOSITION

// NOUN-PHRASE --------------------------------------------------
?noun_phrase: noun | relative | _parens | string | number

number:NUMBER
string:STRING

// relative clause
relative: relative_head "which" subordinate
relative_head:noun_phrase
subordinate: sentence

// noun
noun: (_ARTICLE?|ord?|noun_cmd?|card?|negation?)* adjective* noun_head complement*
noun_head: NAME
card: NUMBER
ord: ORDINAL
noun_cmd:"new"
adjective: /([a-z]+)ful/

// parentheses
_parens: "(" ast ")"

// TERMINALS ----------------------------------------------------
negation: "not"
NAME: /[a-z]+/
NUMBER: /[0-9]+/
ORDINAL: "first" | "last"
BINOP: "and" | "or" | "when" | "after" | "+" | "-" | "*" | "/"
PREPOSITION: "to" | "as" | "on" | "of"
_ARTICLE:"the"|"an"|"a"
DOES: "does" //|"do"
%import common.WS
%import common.ESCAPED_STRING -> STRING
%ignore WS