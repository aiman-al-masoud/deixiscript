// AST ---------------------------------------------------------
?ast: sentence | noun_phrase

// SENTENCE ----------------------------------------------------
?sentence: compound_sentence | simple_sentence

// COMPOUND (AND ALSO COMPLEX) SENTENCE ------------------------
compound_sentence: ast BINOP ast

// SIMPLE-SENTENCE ----------------------------------------------
simple_sentence: _command | _question
_question: subject? _DOES NEGATION* verb object? complement* "?"
_command: subject? cmd NEGATION* verb object? complement*
cmd:_DOES
subject: noun_phrase
verb: WORD
object: noun_phrase
?complement: PREPOSITION noun_phrase?

// NOUN-PHRASE --------------------------------------------------
?noun_phrase: noun | STRING | NUMBER | noun_article | noun_negation | noun_ordinal | noun_cardinal | noun_complement | noun_adjective | noun_relative | _parens

noun_relative: noun_phrase _RELATIVE sentence
noun_adjective: ADJECTIVE noun_phrase
?noun_article: _ARTICLE noun_phrase
noun_negation: NEGATION noun_phrase
noun_complement: noun_phrase complement
noun_ordinal: ORDINAL noun_phrase
noun_cardinal: NUMBER noun_phrase
noun: WORD
ADJECTIVE: /([a-z]+)ful/

// parentheses
_parens: "(" ast ")"

// TERMINALS ----------------------------------------------------
NEGATION: "not"
WORD: /[a-z]+/
NUMBER: /[0-9]+/
ORDINAL: "first" | "last" | "new"
BINOP: "and" | "or" | "when" | "after" | "+" | "-" | "*" | "/"
PREPOSITION: "to" | "as" | "on" | "of"
_ARTICLE:"the"|"an"|"a"
_DOES: "does" //|"do"
_RELATIVE:"which"
%import common.WS
%import common.ESCAPED_STRING -> STRING
%ignore WS